apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: 'codeQuality.gradle'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "info.nukoneko.cuc.android.kidspos"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 6
        versionName "1.0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix = '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        prod {
            dimension "default"
        }
        demo {
            dimension "default"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        animationsDisabled = true
        resultsDir = "$project.buildDir/test-results"
        unitTests.all {
            jvmArgs '-XX:MaxMetaspaceSize=256m'
            systemProperty 'debug', 'true'
        }
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        all {
            java.srcDirs += "src/${name}/kotlin"
        }
        main {
            java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    def kotlinx_coroutines_version = "1.1.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    def koin_version = "1.0.2"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    implementation "android.arch.lifecycle:extensions:1.1.1"

    def support_library_version = "28.0.0"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:preference-v7:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"

    def retrofit_version = "2.5.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation "com.orhanobut:logger:2.2.0"
    implementation 'org.greenrobot:eventbus:3.1.1'

    testImplementation "junit:junit:4.12"
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation "org.mockito:mockito-core:2.23.4"
    testImplementation "org.mockito:mockito-android:2.23.4"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "org.robolectric:robolectric:4.1"
}
