apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: 'codeQuality.gradle'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "info.nukoneko.cuc.android.kidspos"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 6
        versionName "1.0.5"
    }
    dataBinding {
        enabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix = '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        prod {
            dimension "default"
        }
        demo {
            dimension "default"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        animationsDisabled = true
        resultsDir = "$project.buildDir/test-results"
        unitTests.all {
            jvmArgs '-XX:MaxMetaspaceSize=256m'
            systemProperty 'debug', 'true'
        }
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        all {
            java.srcDirs += "src/${name}/kotlin"
        }
        main {
            java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        }
    }
}

dependencies {
    implementation deps.kotlin.core
    implementation deps.kotlin.coroutines.core
    implementation deps.kotlin.coroutines.android
    implementation deps.android.arch.viewModelExtensions
    implementation deps.android.support.appCompat
    implementation deps.android.support.preference
    implementation deps.android.support.recyclerview
    implementation deps.android.support.design
    implementation deps.android.support.cardView
    implementation deps.retrofit.client
    implementation deps.retrofit.converterGson
    implementation deps.retrofit.adapterRxJava
    implementation deps.rx.java2
    implementation deps.rx.android
    implementation deps.loger
    implementation deps.di.koin.core
    implementation deps.di.koin.android
    implementation deps.di.koin.viewModel
    testImplementation deps.test.jUnit
    testImplementation deps.test.mockito
    testImplementation deps.test.mockitoKotlin
    testImplementation deps.test.robolectric
}
